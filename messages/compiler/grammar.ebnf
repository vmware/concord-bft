@@grammar :: CMF
@@comments :: /#.*/

start = {toplevel}+ $ ;

toplevel = msgs:{msg}+ | enums:{enum_def}+ ;

msg = 'Msg' name:msgname id:msgid '{' fields:{field}+ '}' ;
enum_def = 'Enum' name:enumname '{' tags:','.{name}+ '}' ;

msgname = name:name;
enumname = name:name;
msgid = id:number;
field = type:(compound | primitive | toplevel_ref) name:name ;

compound = kvpair:kvpair | list:list | fixedlist:fixedlist | map:map | optional:optional | oneof:oneof ;
kvpair = 'kvpair' key:primitive value:(primitive | compound | toplevel_ref) ;
list = 'list' type:(primitive | compound | toplevel_ref) ;
fixedlist = 'fixedlist' type:(primitive | compound | toplevel_ref) size:number ;
map = 'map' key:primitive value:(primitive | compound | toplevel_ref) ;
optional = 'optional' type:(primitive | compound | toplevel_ref) ;
oneof = 'oneof' '{' msg_names:{toplevel_ref}+ '}' ;

primitive
  =
  | bool
  | integer
  | string
  | bytes
  ;

integer
  =
  | 'uint8'
  | 'uint16'
  | 'uint32'
  | 'uint64'
  | 'int8'
  | 'int16'
  | 'int32'
  | 'int64'
  ;

bool = 'bool';
string = 'string';
bytes = 'bytes';

# A reference to an Enum or Msg used in a field
toplevel_ref = name:name;
name = /[a-zA-Z_][a-zA-Z0-9_]*/ ;
number = /\d+/ ;
